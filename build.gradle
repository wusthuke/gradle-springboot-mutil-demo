buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.4.RELEASE")
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
}

allprojects {
    group = "org.huke.demo.springboot"
    version = "1.0.1"

    ext {
        springBootVersion = "1.5.4.RELEASE"
        springVersion = "4.3.9.RELEASE"
        junitVersion = "4.12"
    }

    task hello {
        doLast { task ->
            println "I'm $task.project.name"
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        // maven { url "http://***" }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$ext.springBootVersion"
        }

        dependencies {
            dependencySet(group: "org.springframework.boot", version: "$ext.springBootVersion") {
                entry "spring-boot-starter-web"
                entry "spring-boot-starter-data-jpa"
                entry "spring-boot-starter-test"
            }

            dependencySet(group: "org.springframework", version: "$ext.springVersion") {
                entry "spring-core"
                entry "spring-beans"
                entry "spring-context"
                entry "spring-test"
            }

            dependencySet(group: "org.mybatis.spring.boot", version: "1.3.0") {
                entry "mybatis-spring-boot-starter"
                entry "mybatis-spring-boot-starter-test"
            }

            dependency group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
            dependency group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
            dependency "junit:junit:$ext.junitVersion"
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                pom.withXml {
                    asNode().appendNode('description', 'A demonstration of Maven POM customization')
                }
            }
        }

        repositories {
            mavenLocal()
            // maven { url "/users/huke/.m2/repository" }
        }
    }

    /*uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://localhost/tmp/myRepo/")
            }
        }
    }*/
}